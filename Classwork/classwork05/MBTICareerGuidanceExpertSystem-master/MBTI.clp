;;; CLIPS Website
;;; clipsrules.net

;;; CLIPS on SourceForge
;;; sourceforge.net/projects/clipsrules/

;;; Adventures in Rule-Based Programming: A CLIPS Tutorial
;;; clipsrules.net/airbp

;;; Original MBTIBasedCareer Source Code
;;; github.com/sulyabtv/MBTICareerGuidanceExpertSystem

(defmodule MAIN (export ?ALL))

;;;   
;;; Defglobals and Deffunctions
;;;

(defglobal ?*fact-priority* = 1000)

(deffunction next-fact-priority ()
   (bind ?*fact-priority* (- ?*fact-priority* 1))
   ?*fact-priority*)

(deffunction ask-question (?id ?prefix $?options)
  (println)
  (println ?id ") " ?prefix)
  (foreach ?o ?options
     (println "   " ?o-index " - " ?o))
  (bind ?length (length$ ?options))
  (if (eq ?length 2)
     then
     (bind ?str "(1 or 2)")
     else
     (bind ?str (str-cat "(1 - " ?length ")")))
   (print "Your response " ?str ": ")
   (bind ?response (read))
   (while (not (and (integerp ?response)
                    (> ?response 0)
                    (<= ?response ?length)))
      (print "Your response " ?str ": ")
      (bind ?response (read)))
   (return ?response))

;;;
;;; Deftemplates
;;;
   
(deftemplate question
   (slot id)
   (slot priority (default-dynamic (next-fact-priority)))
   (slot prefix)
   (multislot options)
   (multislot score)
   (multislot value))
   
(deftemplate score 
   (slot id)
   (slot value (default 0))
   (multislot contributors))

(deftemplate add-score 
   (slot id)
   (slot value (default 0))
   (slot contributor))

(deftemplate pairing
   (slot pos)
   (slot 1st)
   (slot 2nd))

(deftemplate MBTI 
   (slot type (allowed-values ESFJ ESFP ESTJ ESTP
                              ENFJ ENFP ENTJ ENTP
                              ISFJ ISFP ISTJ ISTP
                              INFJ INFP INTJ INTP)))
   
(deftemplate performance
   (slot id
      (allowed-values languages mathematics physics chemistry biology
                      programming sociology art acting music))
   (slot priority (default-dynamic (next-fact-priority)))
   (slot question)
   (slot value (default unspecified))
   (slot threshold (default 3)))
   
(deftemplate career
   (slot id (default-dynamic (gensym*)))
   (slot type (allowed-values ESFJ ESFP ESTJ ESTP
                              ENFJ ENFP ENTJ ENTP
                              ISFJ ISFP ISTJ ISTP
                              INFJ INFP INTJ INTP))
   (slot priority (default-dynamic (next-fact-priority)))
   (multislot needs
      (allowed-values languages mathematics physics chemistry biology
                      programming sociology art acting music))
   (multislot values))

;;;
;;; Defrules
;;;

(defrule welcome
   =>
   (println "Welcome to MBTI-powered Career Guidance System")
   (focus CLASSIFY PERFORMANCE RECOMMEND))

(defmodule CLASSIFY (import MAIN ?ALL))

(defrule ask-question
   ?q <- (question (id ?id)
                   (priority ?priority)
                   (prefix ?prefix)
                   (options $?options)
                   (score $?scores)
                   (value $?values))
   (not (question (priority ?p2&:(> ?p2 ?priority))))
   =>
   (retract ?q)
   (bind ?response (ask-question ?id ?prefix ?options))
   (assert (add-score (id (nth$ ?response ?scores))
                      (value (nth$ ?response ?values))
                      (contributor ?id))))

(defrule add-score
   ?a <- (add-score (id ?id) 
                    (value ?add)
                    (contributor ?contributor))
   ?s <- (score (id ?id)
                (value ?value)
                (contributors $?contributors))
   =>
   (retract ?a)
   (modify ?s (value (+ ?value ?add))
              (contributors ?contributors ?contributor)))

(defrule classify
   (not (question))
   (not (add-score))
   (pairing (pos 1) (1st ?id1) (2nd ?id2))
   (score (id ?id1) (value ?v1))
   (score (id ?id2) (value ?v2))
   (test (> ?v1 ?v2))
   (pairing (pos 2) (1st ?id3) (2nd ?id4))
   (score (id ?id3) (value ?v3))
   (score (id ?id4) (value ?v4))
   (test (> ?v3 ?v4))
   (pairing (pos 3) (1st ?id5) (2nd ?id6))
   (score (id ?id5) (value ?v5))
   (score (id ?id6) (value ?v6))
   (test (> ?v5 ?v6))
   (pairing (pos 4) (1st ?id7) (2nd ?id8))
   (score (id ?id7) (value ?v7))
   (score (id ?id8) (value ?v8))
   (test (> ?v7 ?v8))
   =>
   (bind ?type (sym-cat ?id1 ?id3 ?id5 ?id7))
   (assert (MBTI (type ?type)))
   (println)
   (println "Your MBTI type is " ?type "."))

(defmodule PERFORMANCE (import MAIN ?ALL))

(defrule performance-instructions
   =>
   (println)
   (println "Now, enter your average performance score in the following")
   (println "subjects on a scale from 0 (very poor) to 5 (excellent).")
   (println)
   (assert (get-performance)))

(defrule get-performance-score
   (get-performance)
   ?p <- (performance (question ?question) 
                      (priority ?p1)
                      (value unspecified))
   (not (performance (priority ?p2&:(> ?p2 ?p1))
                     (value unspecified)))
   =>
   (printout t ?question " (0 - 5): ")
   (bind ?response (read))
   (while (not (and (integerp ?response)
                    (>= ?response 0)
                    (<= ?response 5))) do
      (printout t ?question " (0 - 5): ")
      (bind ?response (read)))
   (modify ?p (value ?response)))

(defmodule RECOMMEND (import MAIN ?ALL))

(defrule print-career-header
   =>
   (println)
   (println "The best careers for you, in descending order of match are:")
   (assert (list-careers)))

(defrule print-career
   (list-careers)
   (MBTI (type ?type))
   ?c <- (career (id ?id)
                 (priority ?p1)
                 (type ?type)
                 (values $?careers))
   (forall (career (id ?id)
                   (needs $? ?need $?))
           (performance (id ?need)
                        (value ?value&~unspecified)
                        (threshold ?threshold&:(> ?value ?threshold))))
   (not (career (type ?type)
                (priority ?p2&:(> ?p2 ?p1))))
   =>
   (retract ?c)
   (foreach ?v ?careers
      (println "   " ?v)))
   
(defrule prune-career
   (list-careers)
   (MBTI (type ?type))
   ?c <- (career (id ?id)
                 (priority ?p1)
                 (type ?type))
   (exists (career (id ?id)
                   (needs $? ?need $?))
           (performance (id ?need)
                        (value ?value&~unspecified)
                        (threshold ?threshold&:(<= ?value ?threshold))))
   =>
   (retract ?c))

;;;
;;; Deffacts
;;;

(deffacts MAIN::questions
   (question (id Q1)
             (prefix "At a party, you")
             (options "interact with many, including strangers"
                      "interact with a few, known to you")
             (score E I)
             (value 1 1))
   (question (id Q2)
             (prefix "Are you more")
             (options "realistic than speculative"
                      "speculative than realistic")
             (score S N)
             (value 1 1))
   (question (id Q3)
             (prefix "Are you more impressed by")
             (options "principles"
                      "emotions")
             (score T F)
             (value 1 1))
   (question (id Q4)
             (prefix "Do you tend to choose")
             (options "rather carefully"
                      "somewhat impulsively")
             (score J P)
             (value 1 1))
   (question (id Q5)
             (prefix "While in company do you")
             (options "initiate conversation"
                      "wait to be approached")
             (score E I)
             (value 1 1))
   (question (id Q6)
             (prefix "Are you more interested in")
             (options "what is actual"
                      "what is possible")
             (score S N)
             (value 1 1))
   (question (id Q7)
             (prefix "In judging others are you more swayed by")
             (options "laws than circumstances"
                      "circumstances than laws")
             (score T F)
             (value 1 1))
   (question (id Q8)
             (prefix "Do you prefer to work")
             (options "according to deadlines"
                      "whenever you feel like working")
             (score J P)
             (value 1 1))
   (question (id Q9)
             (prefix "Does meeting new people")
             (options "stimulate and energize you"
                      "tax your reserves")
             (score E I)
             (value 1 1))
   (question (id Q10)
             (prefix "In writings do you prefer")
             (options "literal meanings"
                      "figurative meanings")
             (score S N)
             (value 1 1))
   (question (id Q11)
             (prefix "Which do you wish more for yourself?")
             (options "clarity of reason"
                      "strength of compassion")
             (score T F)
             (value 1 1))
   (question (id Q12)
             (prefix "Do you prefer the")
             (options "planned events"
                      "unplanned/unexpected events")
             (score J P)
             (value 1 1))
   (question (id Q13)
             (prefix "Do you prefer having")
             (options "many friends"
                      "few friends with more lengthy contact")
             (score E I)
             (value 1 1))
   (question (id Q14)
             (prefix "Do you prize more in yourself")
             (options "a strong sense of reality"
                      "a vivid imagination")
             (score S N)
             (value 1 1))
   (question (id Q15)
             (prefix "Which is more important?")
             (options "to discuss an issue thoroughly"
                      "to arrive at an agreement on an issue")
             (score T F)
             (value 1 1))
   (question (id Q16)
             (prefix "Is it preferable mostly to")
             (options "make sure things are arranged"
                      "just let things happen")
             (score J P)
             (value 1 1))
   (question (id Q17)
             (prefix "Are you more inclined to be")
             (options "easy to approach"
                      "somewhat reserved")
             (score E I)
             (value 1 1))
   (question (id Q18)
             (prefix "Are you more likely to trust your")
             (options "experience"
                      "gut feeling/guess")
             (score S N)
             (value 1 1))
   (question (id Q19)
             (prefix "Which rules you more?")
             (options "your brain"
                      "your heart")
             (score T F)
             (value 1 1))
   (question (id Q20)
             (prefix "Are you more comfortable with work that is")
             (options "contracted"
                      "done on a casual basis")
             (score J P)
             (value 1 1)))
   
(deffacts MAIN::scores
   (score (id E) (value 0))
   (score (id I) (value 0))
   (score (id S) (value 0))
   (score (id N) (value 0))
   (score (id F) (value 0))
   (score (id T) (value 0))
   (score (id J) (value 0))
   (score (id P) (value 0)))

(deffacts MAIN::pairings
   (pairing (pos 1) (1st E) (2nd I))
   (pairing (pos 1) (1st I) (2nd E))
   (pairing (pos 2) (1st S) (2nd N))
   (pairing (pos 2) (1st N) (2nd S))
   (pairing (pos 3) (1st F) (2nd T))
   (pairing (pos 3) (1st T) (2nd F))
   (pairing (pos 4) (1st J) (2nd P))
   (pairing (pos 4) (1st P) (2nd J)))
      
(deffacts MAIN::performances
   (performance (id languages)
                (question "Languages"))
   (performance (id mathematics)
                (question "Mathematics"))
   (performance (id physics)
                (question "Physics"))
   (performance (id chemistry)
                (question "Chemistry"))
   (performance (id biology)
                (question "Biology"))
   (performance (id programming)
                (question "Computer Science/Programming"))
   (performance (id sociology)
                (question "Social Sciences"))
   (performance (id art)
                (question "Painting/Sketching"))
   (performance (id acting)
                (question "Acting/Dancing"))
   (performance (id music)
                (question "Music")))
   
(deffacts MAIN::careers
   (career (type ESFJ)
           (needs languages physics biology chemistry)
           (values "Family Doctor/Dentist"))
   (career (type ESFJ)
           (needs languages biology sociology)
           (values "Counselor"))
   (career (type ESFJ)
           (needs languages mathematics sociology)
           (values "Human Resources/Marketing"))
   (career (type ESFJ)
           (needs languages mathematics sociology physics)
           (values "Administrative Work"))
   (career (type ESFJ)
           (needs languages biology chemistry)
           (values "Nursing"))
   (career (type ESFJ)
           (needs languages mathematics)
           (values "Accountant"))
   (career (type ESFJ) 
           (values "Teacher/Social Worker"))
   (career (type ESFP)
           (needs acting)
           (values "Actor/Comedian"))
   (career (type ESFP)
           (needs art)
           (values "Artist/Painter"))
   (career (type ESFP)
           (needs languages mathematics sociology)
           (values "Administrative Work"))
   (career (type ESFP)
           (needs languages sociology)
           (values "Social Worker/Counselor"))
   (career (type ESFP)
           (needs art sociology)
           (values "Fashion Designer"))
   (career (type ESFP)
           (needs art physics)
           (values "Photographer"))
   (career (type ESFP)
           (needs art physics)
           (values "Interior Decorator"))
   (career (type ESFP)
           (needs music)
           (values "Musician"))
   (career (type ESFP)
           (values "Teacher/Trainer/Coach"))
   (career (type ESTJ)
           (needs languages mathematics sociology)
           (values "Management"))
   (career (type ESTJ)
           (needs mathematics sociology)
           (values "Economist/Business Analyst"))
   (career (type ESTJ)
           (needs mathematics)
           (values "Banker/Auditor"))
   (career (type ESTJ)
           (needs physics sociology)
           (values "Detective"))
   (career (type ESTJ)
           (needs sociology)
           (values "Judge"))
   (career (type ESTJ)
           (values "Military/Police" 
                   "Teacher/Lecturer"))
   (career (type ESTP)
           (needs mathematics sociology)
           (values "Entrepreneur"))
   (career (type ESTP)
           (needs languages sociology)
           (values "Sales/Marketing"))
   (career (type ESTP)
           (needs acting)
           (values "Actor/Dancer/Comedian"))
   (career (type ESTP)
           (needs physics programming)
           (values "Computer Technician"))
   (career (type ESTP)
           (values "Military/Police"))
   (career (type ENFJ)
           (needs languages biology sociology)
           (values "Psychologist/Counselor"))
   (career (type ENFJ)
           (needs languages sociology)
           (values "Diplomat"
                   "Editor"))
   (career (type ENFJ)
           (needs sociology)
           (values "Politician"))
   (career (type ENFJ)
           (needs languages)
           (values "Writer"))
   (career (type ENFJ)
           (values "Teacher/Professor/Trainer"))
   (career (type ENFP)
           (needs languages biology sociology)
           (values "Psychologist/Counselor"))
   (career (type ENFP)
           (needs languages sociology)
           (values "Lawyer/Attorney"
                   "Diplomat"))
   (career (type ENFP)
           (needs languages)
           (values "Writer"
                   "Journalist"))
   (career (type ENFP)
           (needs sociology)
           (values "Politician"
                   "Social Worker"))
   (career (type ENFP)
           (values "Teacher/Professor"))
   (career (type ENTJ)
           (needs mathematics physics chemistry)
           (values "Scientist"))
   (career (type ENTJ)
           (needs mathematics sociology)
           (values "Corporate Executive" 
                   "Entrepreneur"))
   (career (type ENTJ)
           (needs mathematics languages)
           (values "Systems Analyst"))
   (career (type ENTJ)
           (needs sociology)
           (values "Politician"))
   (career (type ENTJ)
           (values "Teacher/Professor"))
   (career (type ENTP)
           (needs mathematics physics chemistry)
           (values "Scientist"))
   (career (type ENTP)
           (needs mathematics sociology)
           (values "Entrepreneur"))
   (career (type ENTP)
           (needs languages sociology)
           (values "Lawyer/Attorney"))
   (career (type ENTP)
           (needs acting)
           (values "Actor/Dancer"))
   (career (type ENTP)
           (needs art)
           (values "Artist/Designer"
                   "Photographer"))
   (career (type ENTP)
           (needs programming)
           (values "Computer Engineer"))
   (career (type ENTP)
           (needs music)
           (values "Musician"))
   (career (type ENTP)
           (needs languages)
           (values "Writer"))
   (career (type ENTP)
           (values "Teacher/Professor"))
   (career (type ISFJ)
           (needs languages biology chemistry)
           (values "Doctor"))
   (career (type ISFJ)
           (needs languages biology)
           (values "Nurse/Health Service"))
   (career (type ISFJ)
           (needs languages biology sociology)
           (values "Psychologist/Counselor"))
   (career (type ISFJ)
           (needs sociology)
           (values "Military/Police"))
   (career (type ISFJ)
           (values "Teacher/Professor"))
   (career (type ISFP)
           (needs languages)
           (values "Editor"
                   "Writer"))
   (career (type ISFP)
           (needs art)
           (values "Artist/Designer"))
   (career (type ISFP)
           (needs music)
           (values "Composer/Musician"))
   (career (type ISFP)
           (values "Chef"
                   "Forest Ranger"))
   (career (type ISTJ)
           (needs mathematics physics chemistry)
           (values "Scientist"))
   (career (type ISTJ)
           (needs biology chemistry)
           (values "Doctor"))
   (career (type ISTJ)
           (needs mathematics physics)
           (values "Engineer"))
   (career (type ISTJ)
           (needs languages sociology)
           (values "Judge"
                   "Lawyer/Attorney"))
   (career (type ISTJ)
           (needs mathematics)
           (values "Accountant/Auditor"))
   (career (type ISTJ)
           (needs mathematics sociology)
           (values "Administrative Writer"))
   (career (type ISTJ)
           (needs mathematics programming)
           (values "Computer Engineer"))
   (career (type ISTJ)
           (needs sociology)
           (values "Detective"))
   (career (type ISTJ)
           (values "Military/Police" 
                   "Teacher/Professor"))
   (career (type ISTP)
           (needs mathematics sociology)
           (values "Business Analyst"))
   (career (type ISTP)
           (needs mathematics physics)
           (values "Pilot"))
   (career (type ISTP)
           (needs mathematics programming)
           (values "Computer Engineer"))
   (career (type ISTP)
           (needs mathematics physics chemistry)
           (values "Scientist"))
   (career (type ISTP)
           (needs mathematics physics)
           (values "Engineer"))
   (career (type ISTP)
           (values "Farmer/Planter" 
                   "Athlete" 
                   "Military/Police" 
                   "Teacher/Professor"))
   (career (type INFJ)
           (needs acting)
           (values "Actor"))
   (career (type INFJ)
           (needs art)
           (values "Photographer"
                   "Artist/Designer"))
   (career (type INFJ)
           (needs biology chemistry)
           (values "Doctor/Dentist"))
   (career (type INFJ)
           (needs languages)
           (values "Writer"))
   (career (type INFJ)
           (needs music)
           (values "Musician"))
   (career (type INFJ)
           (values "Teacher/Professor"))
   (career (type INFP)
           (needs music)
           (values "Musician"))
   (career (type INFP)
           (values "Film maker"))
   (career (type INFP)
           (needs art)
           (values "Photographer"
                   "Artist/Designer"))
   (career (type INFP)
           (needs languages biology)
           (values "Psychologist/Counselor"))
   (career (type INFP)
           (needs sociology languages)
           (values "Social Worker/Activist"))
   (career (type INFP)
           (needs languages)
           (values "Writer/Journalist"))
   (career (type INFP)
           (needs art sociology)
           (values "Fashion Designer"))
   (career (type INFP)
           (needs art programming)
           (values "Web Designer"))
   (career (type INFP)
           (needs acting)
           (values "Actor/Dancer"))
   (career (type INFP)
           (values "Teacher/Professor"))
   (career (type INTJ)
           (needs mathematics)
           (values "Business Administrator" 
                   "Corporate Executive"))
   (career (type INTJ)
           (needs mathematics sociology)
           (values "Entrepreneur"))
   (career (type INTJ)
           (needs languages sociology)
           (values "Lawyer/Attorney"))
   (career (type INTJ)
           (needs sociology)
           (values "Politician"))
   (career (type INTJ)
           (needs mathematics physics chemistry)
           (values "Scientist"))
   (career (type INTJ)
           (needs mathematics programming)
           (values "Systems Analyst"))
   (career (type INTJ)
           (values "Teacher/Professor"))
   (career (type INTP)
           (needs acting)
           (values "Actor/Dancer"))
   (career (type INTP)
           (needs art mathematics)
           (values "Architect"))
   (career (type INTP)
           (needs art)
           (values "Artist/Designer"))
   (career (type INTP)
           (needs mathematics physics chemistry)
           (values "Scientist"))
   (career (type INTP)
           (needs physics mathematics)
           (values "Engineer"
                   "Inventor"))
   (career (type INTP)
           (needs mathematics)
           (values "Economist"
                   "Mathematician"))
   (career (type INTP)
           (needs programming mathematics)
           (values "Computer Engineer"))
   (career (type INTP)
           (needs sociology)
           (values "Investigator"))
   (career (type INTP)
           (needs music)
           (values "Musician"))
   (career (type INTP)
           (needs sociology)
           (values "Strategist"))
   (career (type INTP)
           (values "Teacher/Professor")))
